// Example: Polyphony
//
// Using the merge() and poly() functions, we can combine
// sequences together to create polyphonic sequences that
// play independently of one another.

// Here we have a bassline generator that plays notes
// for a full half note.
bass := gen () {
  loop {
    notePitch := scale(SCALE_PENT_MAJOR, G1, rand(6));

    // Double up the octaves
    yield [note [notePitch, HALF], note [notePitch + 12, HALF]];
  }
};

// The melody plays much quicker around an octave higher
// than the bass.
melody := gen () {
  loop {
    noteDuration := n16 * rrand(1, 4);
    notePitch := scale(SCALE_MAJOR, G3, 4 + rand(10));
    yield note [notePitch, noteDuration];
  }
};

main := gen () {
  // poly(...seq) creates a new sequence from whatever
  // sequences you provide the function.
  m := poly(bass(), melody());
  loop {
    n := next m;
    yield n;
  }
};
